/*
* Copyright (c) 2017-2022 The Forge Interactive Inc.
*
* This is a part of Ephemeris.
* This file(code) is licensed under a Creative Commons Attribution-NonCommercial 4.0 International License (https://creativecommons.org/licenses/by-nc/4.0/legalcode) Based on a work at https://github.com/ConfettiFX/The-Forge.
* You can not use this code for commercial purposes.
*
*/

#include "TerrainCommon.h"

#define USE_PROJECTED_SHADOW                 1
#define _EARTH_RADIUS				         5000000.0f                         // Currently, follow the g_EarthRadius in Sky.cpp
#define _EARTH_RADIUS_ADD_CLOUDS_LAYER_START 5015000.0f                         // EARTH_RADIUS + _CLOUDS_LAYER_START

STRUCT(PsIn)
{
	DATA(float4, Position,  SV_Position);
	DATA(float2, ScreenPos, TEXCOORD0);
};

//Code from https://area.autodesk.com/blogs/game-dev-blog/volumetric-clouds/.
bool ray_trace_sphere(float3 center, float3 rd, float3 offset, float radius2, out(float) t1, out(float) t2)
{
	t1 = 0.0f;
	t2 = 0.0f;

	float3 p = center - offset;
	float  b = dot(p, rd);
	float  c = dot(p, p) - radius2;
	float  f = b * b - c;
	
	if (f >= 0.0f)
	{
		float sqrtF = sqrt(f);
		t1 = -b - sqrtF;
		t2 = -b + sqrtF;
	}

	return t1 > 0.0f || t2 > 0.0f;
}

float EvaluateCloudShadow(float3 ws_pos, float3 lightDir)
{
	float2 uv;

#if USE_PROJECTED_SHADOW
	float it1, it2;
	float3 earthCenter = float3(0.0f, -_EARTH_RADIUS, 0.0f);

	ray_trace_sphere(ws_pos.xyz, lightDir, earthCenter, _EARTH_RADIUS_ADD_CLOUDS_LAYER_START, it1, it2);

	float3 CloudPos = ws_pos.xyz + lightDir * it2;
	
	uv = (CloudPos.xz + Get(StandardPosition).xz) / Get(SettingInfo00).z;
	float3 weatherData = SampleLvlTex2D(Get(weatherTexture), Get(g_LinearWrap), uv, 0).rgb;
	
	float result  = saturate(saturate(weatherData.b - Get(SettingInfo00).y) + Get(StandardPosition).w);
	float result2 = result * result;

	return result2 * result2;
#else
	uv = (ws_pos.xz + Get(StandardPosition).xz) / Get(SettingInfo00).z;
	float3 weatherData = SampleLvlTex2D(Get(weatherTexture), Get(g_LinearWrap), uv, 0).rgb;
	
	return saturate(weatherData.g + Get(StandardPosition).w);
#endif
}

float4 PS_MAIN(PsIn In)
{
	INIT_MAIN;

	float2 texCoord = In.ScreenPos * 0.5f - 0.5f;
	texCoord.x = 1.0f - texCoord.x;

	float3 albedo = SampleTex2D(Get(BasicTexture),  Get(g_LinearMirror), texCoord).rgb;
	float3 normal = SampleTex2D(Get(NormalTexture), Get(g_LinearMirror), texCoord).xyz;
  
	float lighting = saturate(dot(normal, Get(LightDirection).xyz));

	float2 texCoord2 = float2((In.ScreenPos.x + 1.0f) * 0.5f, (1.0f - In.ScreenPos.y) * 0.5f);

	float4 ws_pos = mul(Get(InvViewProjMat), float4(In.ScreenPos.xy, SampleTex2D(Get(depthTexture), Get(g_LinearWrap), texCoord2).r, 1.0f));
 
	float shadow_atten_from_clouds = EvaluateCloudShadow(ws_pos.xyz, Get(LightDirection).xyz);
	//return float4(shadow_atten_from_clouds, shadow_atten_from_clouds, shadow_atten_from_clouds, shadow_atten_from_clouds);

	float4 Out = float4(lighting * albedo * Get(SunColor).rgb * Get(LightColor).rgb * Get(LightColor).a * shadow_atten_from_clouds + albedo * 0.1f, 1.0f);
	
	RETURN(Out); 
}
