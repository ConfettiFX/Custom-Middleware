/*
* Copyright (c) 2017-2024 The Forge Interactive Inc.
*
* This is a part of Ephemeris.
* This file(code) is licensed under a Creative Commons Attribution-NonCommercial 4.0 International License (https://creativecommons.org/licenses/by-nc/4.0/legalcode) Based on a work at https://github.com/ConfettiFX/The-Forge.
* You can not use this code for commercial purposes.
*
*/

#include "RenderSky.h"

STRUCT(VsOut)
{
	DATA(float4, Position,    SV_Position);
	DATA(float3, TexCoord,    TEXCOORD0);
	DATA(float2, ScreenCoord, TEXCOORD1);
};

VsOut VS_MAIN(SV_VertexID(uint) VertexID)
{
	INIT_MAIN;

	VsOut Out;
	
	float4 position;
	position.x  = (VertexID == 0) ? 3.0f : -1.0f;
	position.y  = (VertexID == 2) ? 3.0f : -1.0f;
	position.zw = f2(1.0f);

	Out.Position = position;
	
	//	Unproject position
	position.z = 0.0f;
	position.w = Get(QNNear).z;
	position.xy *= f2(position.w);

	Out.TexCoord = mul(Get(invProj), position).xyz;
	Out.TexCoord /= f3(Out.TexCoord.z);

	Out.TexCoord = mul(Get(invView), float4(Out.TexCoord, 0.0f)).xyz;

	Out.ScreenCoord = Out.Position.xy * float2(0.5f, -0.5f) + 0.5f;

	float3 ray = Get(lightDirection).xyz;
	float3 x   = Get(CameraPosition).xyz;
	float3 v   = normalize(ray);

	float r  = length(x);
	float mu = dot(x, v) / r;

	float4 dayLight   = float4(transmittance(r, mu), 1.0f);
	float4 nightLight = Get(LightIntensity) * 0.05f;

	// use lookup transmittance color during sunset
	Get(TransmittanceColor)[VertexID] = Get(lightDirection).y >= 0.2f
                                      ? dayLight 
                                      : lerp(nightLight, dayLight, saturate(Get(lightDirection).y / Get(InScatterParams).z));

	RETURN(Out);
}